buildscript {
    repositories {
        maven { url 'https://artifactory.ida.digital.cabinet-office.gov.uk/artifactory/whitelisted-repos' }
    }
    dependencies {
        classpath 'com.github.spullara.mustache.java:compiler:0.8.10',
                'org.yaml:snakeyaml:1.10',
                'uk.gov.ida:ida-gradle:1.1.0-20',
                'com.google.guava:guava:14.0.1',
                'com.github.ben-manes:gradle-versions-plugin:0.17.0'
    }
}

apply from: 'idea.gradle'
apply plugin: 'java'
apply plugin: 'com.github.ben-manes.versions'

ext {
    opensaml_version3 = '3.3.0'
}

def dependencyVersions = [
            ida_utils:'323',
            // rest_utils has been added to dependency temporarily for enabling Hub to send events using event emitter.
            // Upgrading ida_utils to the latest version breaks verify-hub. The fix for this issue is still being developed.
            // Once the fix is completed, rest_utils will be removed from the dependency.
            rest_utils:'332',
            dropwizard:'1.1.4',
            dropwizard_infinispan:'1.1.4-41',
            pact:'3.5.6',
            stub_idp_saml:"$opensaml_version3-68",
            ida_test_utils:"2.0.0-38",
            opensaml:"$opensaml_version3",
            dev_pki: '1.1.0-21',
            saml_metadata_bindings:"$opensaml_version3-104",
            hub_saml:"$opensaml_version3-120"
        ]

subprojects {
    apply plugin: 'java'
    apply plugin: 'idaJar'

    group = "uk.gov.ida"
    version = "0.1.$version"

    repositories {
        maven { url 'https://artifactory.ida.digital.cabinet-office.gov.uk/artifactory/whitelisted-repos' }
    }

    configurations.all {
        resolutionStrategy {
            // dropwizard-testing uses 2.2.0 but our libraries currently only work with 1.6.0.
            force 'org.assertj:assertj-core:1.6.0',
                    'org.glassfish.hk2:hk2-api:2.4.0-b31',
                    'org.glassfish.hk2:hk2-locator:2.4.0-b31',
                    'org.glassfish.hk2.external:javax.inject:2.4.0-b31',
                    'com.hubspot.dropwizard:dropwizard-guicier:0.9.1.0'
        }
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }

    configurations {
        all*.exclude group: 'org.mockito', module: 'mockito-all'
        common
        dropwizard
        msa
        saml
        stub_saml_test
        saml_test
        pact_test
        soap
        test_deps_compile
        test_deps_runtime
        dropwizard_infinispan
        integrationtests
        config
        rest_utils
        verify_event_emitter
        ida_utils
        ida_test_utils
        dev_pki
        trust_anchor
    }

    dependencies {

        common 'joda-time:joda-time:2.3'

        dropwizard 'io.dropwizard:dropwizard-core:' + dependencyVersions.dropwizard,
                'io.dropwizard:dropwizard-client:' + dependencyVersions.dropwizard,
                'io.dropwizard:dropwizard-auth:' + dependencyVersions.dropwizard,
                'io.dropwizard:dropwizard-configuration:' + dependencyVersions.dropwizard,
                'io.dropwizard:dropwizard-metrics-graphite:' + dependencyVersions.dropwizard,
                'com.hubspot.dropwizard:dropwizard-guicier:0.9.1.0',
                'org.reflections:reflections:0.9.10',
                'com.tradier:dropwizard-raven:1.0.0-1'

        config 'commons-io:commons-io:2.1'

        rest_utils "uk.gov.ida:rest-utils:2.0.0-$dependencyVersions.rest_utils"

        verify_event_emitter "uk.gov.ida:verify-event-emitter:0.0.1-31"

        ida_utils "uk.gov.ida:common-utils:2.0.0-$dependencyVersions.ida_utils",
                "uk.gov.ida:security-utils:2.0.0-$dependencyVersions.ida_utils"

        ida_test_utils "uk.gov.ida:common-test-utils:$dependencyVersions.ida_test_utils"

        dev_pki "uk.gov.ida:ida-dev-pki:$dependencyVersions.dev_pki"

        dropwizard_infinispan "uk.gov.ida:dropwizard-infinispan:$dependencyVersions.dropwizard_infinispan"

        saml "org.opensaml:opensaml-core:$dependencyVersions.opensaml",
             "uk.gov.ida:saml-metadata-bindings:$dependencyVersions.saml_metadata_bindings",
             "uk.gov.ida:hub-saml:$dependencyVersions.hub_saml"

        trust_anchor "uk.gov.ida.eidas:trust-anchor:1.0-19"

        stub_saml_test("uk.gov.ida:stub-idp-saml-test:$dependencyVersions.stub_idp_saml") {
            changing = true
            exclude group: "uk.gov.ida"
        }
        saml_test("uk.gov.ida:hub-saml-test-utils:$dependencyVersions.hub_saml") {
            changing = true
            exclude group: "uk.gov.ida"
        }

        pact_test "au.com.dius:pact-jvm-consumer-junit_2.11:$dependencyVersions.pact",
                "au.com.dius:pact-jvm-consumer_2.11:$dependencyVersions.pact",
                "au.com.dius:pact-jvm-model:$dependencyVersions.pact"
        pact_test("au.com.dius:pact-jvm-provider-junit_2.11:$dependencyVersions.pact") {
            exclude group: 'com.google.guava', module: 'guava'
        }

        def test_deps_deps = [
                'junit:junit:4.11',
                'org.assertj:assertj-core:1.6.0',
                'org.assertj:assertj-joda-time:1.1.0',
                'org.assertj:assertj-guava:1.1.0',
                'org.mockito:mockito-core:1.9.5',
                'org.json:json:20170516',
                'com.jayway.awaitility:awaitility:1.6.0',
                'com.github.tomakehurst:wiremock:2.16.0',
                "uk.gov.ida:saml-metadata-bindings-test:$dependencyVersions.saml_metadata_bindings",
                'io.dropwizard:dropwizard-testing:' + dependencyVersions.dropwizard]

        test_deps_deps.each { dep ->
            test_deps_runtime(dep)
            test_deps_compile(dep) { transitive = false }
        }

        soap('org.apache.ws.commons:ws-commons-util:1.0.1') { transitive = false }
    }
}

task(outputDependencies) doLast {
    println "hub_saml="+dependencyVersions.hub_saml
    println "saml_metadata="+dependencyVersions.saml_metadata
    println "rest_utils="+dependencyVersions.rest_utils
    println "ida_utils="+dependencyVersions.ida_utils
    println "dropwizard_infinispan="+dependencyVersions.dropwizard_infinispan
}

import javax.crypto.Cipher
task(checkJceInstalled) doLast {
  if (Cipher.getMaxAllowedKeyLength('AES') <= 128) {
    throw new GradleException("""ERROR: You need to have the unlimited strength JCE installed.

See http://www.oracle.com/technetwork/java/javase/downloads/jce8-download-2133166.html

+Note this is installed by default in Java 8 1.6.1 or later""")
  }
}

defaultTasks 'clean', 'test', 'jar'
