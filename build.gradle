buildscript {
    repositories {
        if (System.getenv('VERIFY_USE_PUBLIC_BINARIES') == 'true') {
          logger.warn('Production builds MUST NOT be built with public binaries.\nUse artifactory/whitelisted-repos for production builds.\n\n')
          maven { url 'https://dl.bintray.com/alphagov/maven-test' }
          maven { url 'https://build.shibboleth.net/nexus/content/repositories/releases' }
          jcenter()
        }
        else {
          maven { url 'https://artifactory.ida.digital.cabinet-office.gov.uk/artifactory/whitelisted-repos' }
        }
    }
    dependencies {
        classpath 'com.github.spullara.mustache.java:compiler:0.8.10',
                'org.yaml:snakeyaml:1.10',
                'uk.gov.ida:ida-gradle:1.1.0-24',
                'com.google.guava:guava:23.0',
                'com.github.ben-manes:gradle-versions-plugin:0.20.0'
    }
}

apply from: 'idea.gradle'
apply plugin: 'java'
apply plugin: 'com.github.ben-manes.versions'

allprojects {
    apply plugin: 'jacoco'
}

repositories {
    jcenter()
}

ext {
    opensaml_version = '3.4.2'
    dropwizard_version = '1.3.9'
    build_version = "$opensaml_version-${System.env.BUILD_NUMBER ?: 'SNAPSHOT'}"
}

def dependencyVersions = [
            ida_utils:'370',
            dropwizard:"$dropwizard_version",
            dropwizard_infinispan:"$dropwizard_version-48",
            pact:'3.5.6',
            ida_test_utils:"2.0.0-46",
            opensaml:"$opensaml_version",
            dev_pki: '1.1.0-37',
            saml_lib:"$opensaml_version-200"
        ]

subprojects {
    apply plugin: 'java'

    group = "uk.gov.ida"
    version = "0.1.$version"

    repositories {
        if (System.getenv('VERIFY_USE_PUBLIC_BINARIES') == 'true') {
            logger.warn('Production builds MUST NOT be built with public binaries.\nUse artifactory/whitelisted-repos for production builds.\n\n')
            maven { url 'https://dl.bintray.com/alphagov/maven' } // For dropwizard-logstash
            maven { url 'https://dl.bintray.com/alphagov/maven-test' } // For other public verify binaries
            maven { url 'https://build.shibboleth.net/nexus/content/repositories/releases' }
            maven { url 'https://splunk.jfrog.io/splunk/ext-releases-local' }
            jcenter()
        }
        else {
            maven { url 'https://artifactory.ida.digital.cabinet-office.gov.uk/artifactory/whitelisted-repos' }
        }
    }

    configurations.all {
        resolutionStrategy {
            force 'org.glassfish.hk2:hk2-api:2.4.0-b31',
                    'org.glassfish.hk2:hk2-locator:2.4.0-b31',
                    'org.glassfish.hk2.external:javax.inject:2.4.0-b31',
                    'com.hubspot.dropwizard:dropwizard-guicier:0.9.1.0'
        }
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }

    configurations {
        all*.exclude group: 'org.mockito', module: 'mockito-all'
        common
        dropwizard
        msa
        saml
        saml_test
        pact_test
        soap
        test_deps_compile
        test_deps_runtime
        dropwizard_infinispan
        integrationtests
        config
        verify_event_emitter
        ida_utils
        ida_test_utils
        dev_pki
        saml_lib
        prometheus
        redis
        redis_test
        splunk
        awssdk
        s3mock
    }

    dependencies {

        common 'joda-time:joda-time:2.3',
                'com.google.inject:guice:4.0',
                "javax.xml.bind:jaxb-api:2.3.1",
                "com.sun.xml.bind:jaxb-core:2.3.0.1",
                "com.sun.xml.bind:jaxb-impl:2.3.0.1"

        dropwizard 'io.dropwizard:dropwizard-core:' + dependencyVersions.dropwizard,
                'io.dropwizard:dropwizard-client:' + dependencyVersions.dropwizard,
                'io.dropwizard:dropwizard-auth:' + dependencyVersions.dropwizard,
                'io.dropwizard:dropwizard-configuration:' + dependencyVersions.dropwizard,
                'io.dropwizard:dropwizard-metrics-graphite:' + dependencyVersions.dropwizard,
                'com.hubspot.dropwizard:dropwizard-guicier:1.0.0.6',
                'org.reflections:reflections:0.9.10',
                'com.tradier:dropwizard-raven:1.0.0-1'

        config 'commons-io:commons-io:2.1'

        verify_event_emitter "uk.gov.ida:verify-event-emitter:0.0.1-67"

        ida_utils "uk.gov.ida:common-utils:2.0.0-$dependencyVersions.ida_utils",
                "uk.gov.ida:security-utils:2.0.0-$dependencyVersions.ida_utils",
                "uk.gov.ida:rest-utils:2.0.0-$dependencyVersions.ida_utils"

        // To resolve an issue with httpclient:4.5.7 introduced via dropwizard:1.3.9 with dropping consecutive slashes in a URI.
        ida_utils("org.apache.httpcomponents:httpclient:4.5.6") {
            force = true
        }

        ida_test_utils ("uk.gov.ida:common-test-utils:$dependencyVersions.ida_test_utils"){
            exclude group: 'org.assertj', module: 'assertj'
            exclude group: 'org.assertj', module: 'assertj-core'
        }

        dev_pki "uk.gov.ida:ida-dev-pki:$dependencyVersions.dev_pki"

        dropwizard_infinispan "uk.gov.ida:dropwizard-infinispan:$dependencyVersions.dropwizard_infinispan"

        prometheus 'engineering.gds-reliability:gds-metrics-dropwizard:0.1.0'

        saml "org.opensaml:opensaml-core:$dependencyVersions.opensaml",
             project(":hub-saml")

        saml_lib "uk.gov.ida:saml-lib:$dependencyVersions.saml_lib"

        saml_test "uk.gov.ida:saml-test:$dependencyVersions.saml_lib"

        pact_test "au.com.dius:pact-jvm-consumer-junit_2.11:$dependencyVersions.pact",
                "au.com.dius:pact-jvm-consumer_2.11:$dependencyVersions.pact",
                "au.com.dius:pact-jvm-model:$dependencyVersions.pact"
        pact_test("au.com.dius:pact-jvm-provider-junit_2.11:$dependencyVersions.pact") {
            exclude group: 'com.google.guava', module: 'guava'
        }

        def test_deps_deps = [
                'org.json:json:20170516',
                'com.jayway.awaitility:awaitility:1.6.0',
                'com.github.tomakehurst:wiremock-standalone:2.23.2',
                "uk.gov.ida:saml-test:$dependencyVersions.saml_lib",
                'org.assertj:assertj-core:3.9.1',
                'io.dropwizard:dropwizard-testing:' + dependencyVersions.dropwizard]

        test_deps_deps.each { dep ->
            test_deps_runtime(dep)
            test_deps_compile(dep) { transitive = false }
        }

        soap('org.apache.ws.commons:ws-commons-util:1.0.1') { transitive = false }

        redis('io.lettuce:lettuce-core:5.1.4.RELEASE')
        redis_test('com.github.kstyrc:embedded-redis:0.6')

        splunk('uk.gov.ida:splunk-library-javalogging:1.1.0')

        awssdk 'com.amazonaws:aws-java-sdk-s3:1.11.563'

        s3mock 'com.adobe.testing:s3mock:1.1.6'
    }
}

task jacocoMerge(type: JacocoMerge) {
    destinationFile = file("$buildDir/jacoco/allTestCoverage.exec")
    executionData = project.fileTree(dir: '.', include:'**/build/jacoco/*est.exec')
    jacocoClasspath = project.files(project.configurations.jacocoAnt)
}

task jacocoRootReport(type: JacocoReport) {
    dependsOn jacocoMerge

    additionalSourceDirs files(subprojects.sourceSets.main.allSource.srcDirs)
    additionalClassDirs files(subprojects.sourceSets.main.output)
    executionData jacocoMerge.destinationFile

    reports {
        html.enabled = true
        xml.enabled = true
    }

    doFirst {
        executionData files(executionData.findAll { it.exists() })
    }
}

task downloadDependencies(type: Exec) {
    configurations.testRuntime.files
    commandLine 'echo', 'Downloaded all dependencies'
}

task(outputDependencies) doLast {
    println "hub_saml="+dependencyVersions.hub_saml
    println "saml_metadata="+dependencyVersions.saml_metadata
    println "rest_utils="+dependencyVersions.rest_utils
    println "ida_utils="+dependencyVersions.ida_utils
    println "dropwizard_infinispan="+dependencyVersions.dropwizard_infinispan
}

import javax.crypto.Cipher
task(checkJceInstalled) doLast {
  if (Cipher.getMaxAllowedKeyLength('AES') <= 128) {
    throw new GradleException("""ERROR: You need to have the unlimited strength JCE installed.

See http://www.oracle.com/technetwork/java/javase/downloads/jce8-download-2133166.html

+Note this is installed by default in Java 8 1.6.1 or later""")
  }
}

defaultTasks 'clean', 'test', 'jar'
